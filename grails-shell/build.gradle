apply plugin:'application'

mainClassName = "org.grails.cli.GrailsCli"

repositories { 
    jcenter()
}

ext {
    gradleToolingApiVersion = "2.3-20141120230022+0000" // gradle.gradleVersion
}

dependencies {
    compile project(":grails-bootstrap")
    compile "org.apache.ant:ant:$antVersion"
    compile "org.codehaus.groovy:groovy-ant:$groovyVersion"
    compile "org.codehaus.groovy:groovy-json:$groovyVersion"
    compile "org.fusesource.jansi:jansi:$jansiVersion"
    compile "jline:jline:$jlineVersion"
    compile "org.eclipse.jgit:org.eclipse.jgit:3.5.2.201411120430-r"
    compile "org.gradle:gradle-tooling-api:$gradleToolingApiVersion"

    testCompile "net.sf.expectit:expectit-core:0.5.0"
    testCompile "com.github.jnr:jnr-posix:3.0.6"

    runtime "org.slf4j:slf4j-simple:$slf4jVersion"
}

eclipse {
    classpath {
       file {
           whenMerged { classpath ->
                classpath.entries.find { entry -> entry.kind == 'src' && entry.path == "src/test/resources" }?.excludes=["profiles-repository/**", "gradle-sample/**"]
           }
       }
    }
}

test {
    // jline doesn't use jline.terminal system property when TERM is dumb so use different TERM value for tests
    // https://github.com/jline/jline2/blob/6a1b6bf/src/main/java/jline/TerminalFactory.java#L54-L57
    environment 'TERM', 'xterm'
}